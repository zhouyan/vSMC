# ============================================================================
#  vSMC/example/CMakeLists.txt
# ----------------------------------------------------------------------------
#                          vSMC: Scalable Monte Carlo
# ----------------------------------------------------------------------------
# Copyright (c) 2013,2014, Yan Zhou
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
#   Redistributions of source code must retain the above copyright notice, this
#   list of conditions and the following disclaimer.
#
#   Redistributions in binary form must reproduce the above copyright notice,
#   this list of conditions and the following disclaimer in the documentation
#   and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
# ============================================================================

PROJECT (vSMCExample C CXX)
INCLUDE_DIRECTORIES (${PROJECT_SOURCE_DIR}/include)

INCLUDE (vSMCExampleFunctions)

# C++ 11 options
OPTION (VSMC_ENABLE_CXX11 "Enable C++11 features and libraries" ON)

# MPI option
OPTION (VSMC_ENABLE_MPI "Enable MPI" OFF)

# Boost
INCLUDE (FindBoost)
IF (Boost_FOUND)
    INCLUDE_DIRECTORIES (SYSTEM ${Boost_INCLUDE_DIRS})
    SET (CMAKE_REQUIRED_INCLUDES ${CMAKE_REQUIRED_INCLUDES}
        ${Boost_INCLUDE_DIRS})
ENDIF (Boost_FOUND)

# Required definitions
SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
    -D__STDC_CONSTANT_MACROS)
SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
    -D__CL_ENABLE_EXCEPTIONS)
IF (APPLE)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS} -U__OBJC__)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS} -U__OBJC2__)
ENDIF (APPLE)

# C++11 <functional>
INCLUDE (vSMCFindFunctional)
IF (NOT VSMC_FUNCTIONAL_FOUND)
    MESSAGE ("NOT Found C++11 or Boost function")
    RETURN ()
ENDIF (NOT VSMC_FUNCTIONAL_FOUND)
IF (VSMC_FUNCTIONAL_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_FUNCTIONAL=1)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_CXX11LIB_FUNCTIONAL=1)
ELSE (VSMC_FUNCTIONAL_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_FUNCTIONAL=0)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_CXX11LIB_FUNCTIONAL=0)
ENDIF (VSMC_FUNCTIONAL_STD_FOUND)

# C++11 <random>
INCLUDE (vSMCFindRandom)
IF (NOT VSMC_RANDOM_FOUND)
    MESSAGE ("NOT Found C++11 or Boost random")
    RETURN ()
ENDIF (NOT VSMC_RANDOM_FOUND)
IF (VSMC_RANDOM_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_RANDOM=1)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_CXX11LIB_RANDOM=1)
ELSE (VSMC_RANDOM_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_RANDOM=0)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_CXX11LIB_RANDOM=0)
ENDIF (VSMC_RANDOM_STD_FOUND)

# C++11 <cmath>
INCLUDE (vSMCFindCXX11Math)
IF (NOT VSMC_CXX11_MATH_FOUND)
    MESSAGE ("NOT Found C++11, C99, or Boost math")
    RETURN ()
ENDIF (NOT VSMC_CXX11_MATH_FOUND)
IF (VSMC_CXX11_MATH_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_CMATH=1)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_CXX11LIB_CMATH=1)
ELSE (VSMC_CXX11_MATH_STD_STD_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_CMATH=0)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_CXX11LIB_CMATH=0)
ENDIF (VSMC_CXX11_MATH_STD_FOUND)
IF (VSMC_CXX11_MATH_C99_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_C99LIB_MATH=1)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_C99LIB_MATH=1)
ELSE (VSMC_CXX11_MATH_C99_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_C99LIB_MATH=0)
    SET (CMAKE_REQUIRED_DEFINITIONS ${CMAKE_REQUIRED_DEFINITIONS}
        -DVSMC_HAS_C99LIB_MATH=0)
ENDIF (VSMC_CXX11_MATH_C99_FOUND)

##############################################################################
# Check backends
##############################################################################

# Sequential
SET (BACKENDS "Sequential")
SET (SMP_EXECUTABLES seq)

# Intel Cilk Plus
IF (${CMAKE_CXX_COMPILER_ID} MATCHES "Intel")
    INCLUDE (FindCilk)
ENDIF (${CMAKE_CXX_COMPILER_ID} MATCHES "Intel")
IF (CILK_FOUND)
    SET (BACKENDS ${BACKENDS} "Intel Cilk Plus")
    SET (SMP_EXECUTABLES ${SMP_EXECUTABLES} cilk)
    ADD_DEFINITIONS (-DVSMC_HAS_CILK=1)
ELSE (CILK_FOUND)
    UNSET (CILK_FOUND CACHE)
    SET (CILK_FOUND FALSE CACHE BOOL "NOT Found Intel Cilk Plus")
ENDIF (CILK_FOUND)

# Intel TBB
INCLUDE (FindTBB)
IF (TBB_FOUND)
    SET (BACKENDS ${BACKENDS} "Intel TBB")
    SET (SMP_EXECUTABLES ${SMP_EXECUTABLES} tbb)
    ADD_DEFINITIONS (${TBB_DEFINITIONS})
    ADD_DEFINITIONS (-DVSMC_HAS_TBB=1)
    INCLUDE_DIRECTORIES (SYSTEM ${TBB_INCLUDE_DIR})
ELSE (TBB_FOUND)
    UNSET (TBB_FOUND CACHE)
    SET (TBB_FOUND FALSE CACHE BOOL "NOT Found Intel TBB")
ENDIF (TBB_FOUND)

# OpenMP
IF (NOT ${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
    INCLUDE (vSMCFindOpenMP)
ENDIF (NOT ${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
IF (VSMC_OPENMP_FOUND)
    SET (BACKENDS ${BACKENDS} "OpenMP")
    SET (SMP_EXECUTABLES ${SMP_EXECUTABLES} omp)
    ADD_DEFINITIONS (-DVSMC_HAS_OMP=1)
ELSE (VSMC_OPENMP_FOUND)
    UNSET (VSMC_OPENMP_FOUND CACHE)
    SET (VSMC_OPENMP_FOUND FALSE CACHE BOOL "NOT Found OpenMP")
ENDIF (VSMC_OPENMP_FOUND)

# Apple GCD
INCLUDE (FindGCD)
IF (GCD_FOUND)
    SET (BACKENDS ${BACKENDS} "Apple GCD")
    SET (SMP_EXECUTABLES ${SMP_EXECUTABLES} gcd)
    ADD_DEFINITIONS (-DVSMC_HAS_GCD=1)
    INCLUDE_DIRECTORIES (SYSTEM ${GCD_INCLUDE_DIR})
ELSE (GCD_FOUND)
    UNSET (GCD_FOUND CACHE)
    SET (GCD_FOUND FALSE CACHE BOOL "NOT Found Apple GCD")
ENDIF (GCD_FOUND)

# Microsoft PPL
IF (NOT DEFINED PPL_FOUND)
    IF (MSVC AND NOT MSVC_VERSION LESS 1600)
        MESSAGE (STATUS "Found PPL")
        SET (PPL_FOUND TRUE CACHE BOOL "Found PPL")
    ELSE (MSVC AND NOT MSVC_VERSION LESS 1600)
        SET (PPL_FOUND FALSE CACHE BOOL "NOT Found PPL")
    ENDIF (MSVC AND NOT MSVC_VERSION LESS 1600)
ENDIF (NOT DEFINED PPL_FOUND)
IF (PPL_FOUND)
    SET (BACKENDS ${BACKENDS} "Microsoft PPL")
    SET (SMP_EXECUTABLES ${SMP_EXECUTABLES} ppl)
    ADD_DEFINITIONS (-DVSMC_HAS_PPL=1)
ELSE (PPL_FOUND)
    UNSET (PPL_FOUND CACHE)
    SET (PPL_FOUND FALSE CACHE BOOL "NOT Found Microsoft PPL")
ENDIF (PPL_FOUND)

# C++11 <thread> and <future>
IF (VSMC_ENABLE_CXX11)
    INCLUDE (FindCXX11LibThread)
ENDIF (VSMC_ENABLE_CXX11)
IF (CXX11LIB_THREAD_FOUND)
    SET (BACKENDS ${BACKENDS} "C++11 <thread>")
    SET (SMP_EXECUTABLES ${SMP_EXECUTABLES} std)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_THREAD=1)
    SET (VSMC_LINK_LIBRARIES ${VSMC_LINK_LIBRARIES} ${CMAKE_THREAD_LIBS_INIT})
ELSE (CXX11LIB_THREAD_FOUND)
    UNSET (CXX11LIB_THREAD_FOUND CACHE)
    SET (CXX11LIB_THREAD_FOUND FALSE CACHE BOOL "NOT Found C++11 <thread>")
ENDIF (CXX11LIB_THREAD_FOUND)

# OpenCL
INCLUDE (FindOpenCL)
IF (OPENCL_FOUND)
    SET (BACKENDS ${BACKENDS} "OpenCL")
    ADD_CUSTOM_TARGET (example_cl)
    ADD_DEFINITIONS (${OpenCL_DEFINITIONS})
    ADD_DEFINITIONS (-DVSMC_HAS_OPENCL=1)
    INCLUDE_DIRECTORIES (SYSTEM ${OpenCL_INCLUDE_DIR})
ELSE (OPENCL_FOUND)
    UNSET (OPENCL_FOUND CACHE)
    SET (OPENCL_FOUND FALSE CACHE BOOL "NOT Found OpenCL")
ENDIF (OPENCL_FOUND)

# MPI
IF (VSMC_ENABLE_MPI)
    INCLUDE (vSMCFindMPI)
ENDIF (VSMC_ENABLE_MPI)
IF (VSMC_MPI_FOUND)
    SET (BACKENDS ${BACKENDS} "MPI")
    ADD_CUSTOM_TARGET (example_mpi)
    ADD_DEFINITIONS (-DVSMC_HAS_MPI=1)
    INCLUDE_DIRECTORIES (SYSTEM ${MPI_CXX_INCLUDE_PATH})
ELSE (VSMC_MPI_FOUND)
    UNSET (VSMC_MPI_FOUND CACHE)
    SET (VSMC_MPI_FOUND FALSE CACHE BOOL "NOT Found MPI")
ENDIF (VSMC_MPI_FOUND)

# SMP executables
FOREACH (smp ${SMP_EXECUTABLES})
    ADD_CUSTOM_TARGET (example_${smp})
ENDFOREACH (smp)

##############################################################################
# Check features
##############################################################################

SET (FEATURES)

# C++11 <algorithm>
IF (VSMC_ENABLE_CXX11)
    INCLUDE (FindCXX11LibAlgorithm)
ENDIF (VSMC_ENABLE_CXX11)
IF (CXX11LIB_ALGORITHM_FOUND)
    SET (FEATURES ${FEATURES} "C++11 <algorithm>")
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_ALGORITHM=1)
ELSE (CXX11LIB_ALGORITHM_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_ALGORITHM=0)
    UNSET (CXX11LIB_ALGORITHM_FOUND CACHE)
    SET (CXX11LIB_ALGORITHM_FOUND FALSE CACHE BOOL
        "NOT Found C++11 <algorithm>")
ENDIF (CXX11LIB_ALGORITHM_FOUND)

# C++11 <chrono>
IF (VSMC_ENABLE_CXX11 AND NOT MSVC)
    INCLUDE (FindCXX11LibChrono)
ENDIF (VSMC_ENABLE_CXX11 AND NOT MSVC)
IF (CXX11LIB_CHRONO_FOUND)
    SET (FEATURES ${FEATURES} "C++11 <chrono>")
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_CHRONO=1)
ELSE (CXX11LIB_CHRONO_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_CHRONO=0)
    UNSET (CXX11LIB_CHRONO_FOUND CACHE)
    SET (CXX11LIB_CHRONO_FOUND FALSE CACHE BOOL "NOT Found C++11 <chrono>")
ENDIF (CXX11LIB_CHRONO_FOUND)

# C++11 <tuple>
IF (VSMC_ENABLE_CXX11)
    INCLUDE (FindCXX11LibTuple)
ENDIF (VSMC_ENABLE_CXX11)
IF (CXX11LIB_TUPLE_FOUND)
    SET (FEATURES ${FEATURES} "C++11 <tuple>")
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_TUPLE=1)
ELSE (CXX11LIB_TUPLE_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_CXX11LIB_TUPLE=0)
    UNSET (CXX11LIB_TUPLE_FOUND CACHE)
    SET (CXX11LIB_TUPLE_FOUND FALSE CACHE BOOL "NOT Foudn C++11 <tuple>")
ENDIF (CXX11LIB_TUPLE_FOUND)

# CPUID
INCLUDE (FindCPUID)
IF (CPUID_FOUND)
    SET (FEATURES ${FEATURES} "Intel x86 CPUID")
ELSE (CPUID_FOUND)
    UNSET (CPUID_FOUND CACHE)
    SET (CPUID_FOUND FALSE CACHE BOOL "NOT Found CPUID")
ENDIF (CPUID_FOUND)

# RDTCSP
INCLUDE (FindRDTSCP)
IF (RDTSCP_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_RDTSCP=1)
    SET (FEATURES ${FEATURES} "Intel x86 RDTSCP")
ELSE (RDTSCP_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_RDTSCP=0)
    UNSET (RDTSCP_FOUND CACHE)
    SET (RDTSCP_FOUND FALSE CACHE BOOL "NOT Found RDTSCP")
ENDIF (RDTSCP_FOUND)

# 128-bits integer type
INCLUDE (FindInt128)
IF (INT128_FOUND)
    SET (FEATURES ${FEATURES} "128-bits integer type")
    ADD_DEFINITIONS (-DVSMC_HAS_INT128=1)
    ADD_DEFINITIONS (-DVSMC_INT128=${INT128_TYPE})
ELSE (INT128_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_INT128=0)
    UNSET (INT128_FOUND)
    SET (INT128_FOUND FALSE CACHE BOOL "NOT Found int128")
ENDIF (INT128_FOUND)

# TestU01
INCLUDE (FindTestU01)
IF (TESTU01_FOUND)
    SET (FEATURES ${FEATURES} "TestU01")
    INCLUDE_DIRECTORIES (SYSTEM ${TestU01_INCLUDE_DIR})
ELSE (TESTU01_FOUND)
    UNSET (TESTU01_FOUND CACHE)
    SET (TESTU01_FOUND FALSE CACHE BOOL "NOT Found TestU01")
ENDIF (TESTU01_FOUND)

# Random123
IF (OPENCL_FOUND AND RANDOM123_FOUND)
    SET (FEATURES ${FEATURES} "Random123 in OpenCL")
ELSE (OPENCL_FOUND AND RANDOM123_FOUND)
    UNSET (RANDOM123_FOUND CACHE)
    SET (RANDOM123_FOUND FALSE CACHE BOOL "NOT Found RANDOM123 in OpenCL")
ENDIF (OPENCL_FOUND AND RANDOM123_FOUND)
IF (VSMC_RANDOM123_FOUND)
    SET (FEATURES ${FEATURES} "Random123 in C++")
    INCLUDE_DIRECTORIES (SYSTEM ${Random123_INCLUDE_DIR})
    ADD_DEFINITIONS (-DVSMC_HAS_RANDOM123=1)
ELSE (VSMC_RANDOM123_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_RANDOM123=0)
    UNSET (VSMC_RANDOM123_FOUND CACHE)
    SET (VSMC_RANDOM123_FOUND FALSE CACHE BOOL "NOT Found RANDOM123 in C++")
ENDIF (VSMC_RANDOM123_FOUND)

# GSL
INCLUDE (FindGSL)
IF (GSL_FOUND)
    SET (FEATURES ${FEATURES} "GSL")
    INCLUDE_DIRECTORIES (SYSTEM ${GSL_INCLUDE_DIR})
ELSE (GSL_FOUND)
    UNSET (GSL_FOUND CACHE)
    SET (GSL_FOUND FALSE CACHE BOOL "NOT Found GSL")
ENDIF (GSL_FOUND)

# MKL
INCLUDE (FindMKL)
IF (MKL_FOUND)
    SET (FEATURES ${FEATURES} "Intel MKL")
    ADD_DEFINITIONS (-DVSMC_HAS_MKL=1)
    INCLUDE_DIRECTORIES (SYSTEM ${MKL_INCLUDE_DIR})
    SET (VSMC_LINK_LIBRARIES ${VSMC_LINK_LIBRARIES} ${MKL_LINK_LIBRARIES})
ELSE (MKL_FOUND)
    UNSET (MKL_FOUND CACHE)
    SET (MKL_FOUND FALSE CACHE BOOL "NOT Found Intel MKL")
ENDIF (MKL_FOUND)

IF (TBB_FOUND AND TBB_MALLOC_LINK_LIBRARIES)
    SET (FEATURES ${FEATURES} "Intel TBB scalable allocator")
    ADD_DEFINITIONS (-DVSMC_HAS_TBB_MALLOC=1)
    SET (VSMC_LINK_LIBRARIES ${VSMC_LINK_LIBRARIES}
        ${TBB_MALLOC_LINK_LIBRARIES})
ELSE (TBB_FOUND AND TBB_MALLOC_LINK_LIBRARIES)
    ADD_DEFINITIONS (-DVSMC_HAS_TBB_MALLOC=0)
    UNSET (TBB_MALLOC_FOUND CACHE)
    SET (TBB_MALLOC_FOUND FALSE CACHE BOOL "NOT Found Intel TBB malloc")
ENDIF (TBB_FOUND AND TBB_MALLOC_LINK_LIBRARIES)

# AES-NI
INCLUDE (FindAESNI)
IF (AESNI_FOUND)
    SET (FEATURES ${FEATURES} "AES-NI")
    ADD_DEFINITIONS (-DVSMC_HAS_AES_NI=1)
ELSE (AESNI_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_AES_NI=0)
    UNSET (AESNI_FOUND CACHE)
    SET (AESNI_FOUND FALSE CACHE BOOL "NOT Found AES-NI")
ENDIF (AESNI_FOUND)

# RDRAND
INCLUDE (FindRDRAND)
IF (RDRAND_FOUND)
    SET (FEATURES ${FEATURES} "RDRAND")
    ADD_DEFINITIONS (-DVSMC_HAS_RDRAND=1)
ELSE (RDRAND_FOUND)
    ADD_DEFINITIONS (-DVSMC_HAS_RDRAND=0)
    UNSET (RDRAND_FOUND CACHE)
    SET (RDRAND_FOUND FALSE CACHE BOOL "NOT Found RDRAND")
ENDIF (RDRAND_FOUND)

# Linux librt
IF (UNIX AND NOT APPLE AND NOT DEFINED LINUX_LIBRT)
    FIND_LIBRARY (LINUX_LIBRT rt)
ENDIF (UNIX AND NOT APPLE AND NOT DEFINED LINUX_LIBRT)
IF (LINUX_LIBRT)
    SET (FEATURES ${FEATURES} "Linux librt")
    SET (VSMC_LINK_LIBRARIES ${VSMC_LINK_LIBRARIES} ${LINUX_LIBRT})
ENDIF (LINUX_LIBRT)

# HDF5
INCLUDE (FindHDF5)
IF (HDF5_FOUND)
    SET (FEATURES ${FEATURES} "HDF5")
    MESSAGE (STATUS "HDF5 flags (not used): ${HDF5_DEFINITIONS}")
    ADD_DEFINITIONS (-DVSMC_HAS_HDF5=1)
    INCLUDE_DIRECTORIES (SYSTEM ${HDF5_INCLUDE_DIRS})
    SET (VSMC_LINK_LIBRARIES ${VSMC_LINK_LIBRARIES} ${HDF5_LIBRARIES})
ELSE (HDF5_FOUND)
    UNSET (HDF5_FOUND CACHE)
    SET (HDF5_FOUND FALSE CACHE BOOL "NOT Found HDF5")
ENDIF (HDF5_FOUND)

##############################################################################
# Macros
##############################################################################

# Make sure __STDC_CONSTANT_MACROS is defined
ADD_DEFINITIONS (-D__STDC_CONSTANT_MACROS)

# Make sure __CL_ENABLE_EXCEPTIONS is defined if using OpenCL
IF (OPENCL_FOUND)
    ADD_DEFINITIONS (-D__CL_ENABLE_EXCEPTIONS)
ENDIF (OPENCL_FOUND)

# Workaround for vanilla GCC on Mac OS X
IF (APPLE)
    ADD_DEFINITIONS (-U__OBJC__)
    ADD_DEFINITIONS (-U__OBJC2__)
ENDIF (APPLE)

# Disable MSVC iterator warning
IF (MSVC)
    ADD_DEFINITIONS (-D_SCL_SECURE_NO_WARNINGS)
ENDIF (MSVC)

ADD_DEFINITIONS (${EXAMPLE_DEFINITIONS})

##############################################################################
# Check examples
##############################################################################

ADD_CUSTOM_TARGET (example-files)
COPY_FILE (example benchmark.R)
ADD_DEPENDENCIES (example example-files)

SET (EXAMPLES ${EXAMPLES} "vsmc")
ADD_SUBDIRECTORY (vsmc)

SET (EXAMPLES ${EXAMPLES} "paper")
ADD_SUBDIRECTORY (paper)

SET (EXAMPLES ${EXAMPLES} "pf")
ADD_SUBDIRECTORY (pf)

SET (EXAMPLES ${EXAMPLES} "rng")
ADD_SUBDIRECTORY (rng)

SET (EXAMPLES ${EXAMPLES} "node")
ADD_SUBDIRECTORY (node)

IF (VSMC_CXX11_MATH_FOUND)
    SET (EXAMPLES ${EXAMPLES} "gmm")
    ADD_SUBDIRECTORY (gmm)

    SET (EXAMPLES ${EXAMPLES} "pet")
    ADD_SUBDIRECTORY (pet)
ENDIF (VSMC_CXX11_MATH_FOUND)

IF (OPENCL_FOUND)
    SET (EXAMPLES ${EXAMPLES} "opencl")
    ADD_SUBDIRECTORY (opencl)
ENDIF (OPENCL_FOUND)

SET (EXAMPLES ${EXAMPLES} "utility")
ADD_SUBDIRECTORY (utility)

##############################################################################
# Enable backends
##############################################################################

MESSAGE (STATUS "=================== Enable backends ===================")
FOREACH (backend ${BACKENDS})
    MESSAGE (STATUS ${backend})
ENDFOREACH (backend ${BACKENDS})

##############################################################################
# Enable features
##############################################################################

MESSAGE (STATUS "=================== Enable features ===================")
FOREACH (feature ${FEATURES})
    MESSAGE (STATUS ${feature})
ENDFOREACH (feature ${FEATURES})

##############################################################################
# Enable examples
##############################################################################

MESSAGE (STATUS "=================== Enable examples ===================")
FOREACH (example ${EXAMPLES})
    MESSAGE (STATUS ${example})
ENDFOREACH (example ${EXAMPLES})

MESSAGE (STATUS "=======================================================")
